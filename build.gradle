buildscript {

  ext {
    corda_gradle_plugins_version = "5.0.4"
  }

  repositories {
    if( ! new File('libs').isDirectory() ) {
      maven { url "http://si-nexus01.dev.setl.io:8081/repository/maven-public/" }
    }
    mavenCentral()
    jcenter()
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
 }

  dependencies {
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.0'
    classpath "org.owasp:dependency-check-gradle:5.3.2.1"

    classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
    classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
    classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
  }
}

plugins {
  id "com.github.spotbugs" version "4.7.10"
  id 'com.palantir.git-version' version '0.12.3'
  id "io.freefair.lombok" version "5.3.3.3"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: "com.github.spotbugs"
apply plugin: "org.sonarqube"
apply plugin: "org.owasp.dependencycheck"

sourceCompatibility = 11

group 'io.setl.iobc'
if (
(versionDetails().branchName == null || (versionDetails().branchName ==~ /^(master)|(release.*)|(patch.*)$/))
    && versionDetails().isCleanTag
    && gitVersion() ==~ /^\d+(\.\d+)+(-.+)?$/
) {
  project.version = gitVersion()
} else {
  project.version = '100-SNAPSHOT'
}

// Allow publishing from any branch for any tag
if( versionDetails().isCleanTag ) {
  project.version = gitVersion()
}

repositories {
  mavenLocal()
  mavenCentral()

  if( ! new File('libs').isDirectory() ) {
    repositories {
      maven { url "http://si-nexus01.dev.setl.io:8081/repository/maven-public/" }
    }
  }
}

configure(allprojects) { project ->
  ext.bouncyCastleVersion = '1.70'
  ext.dockerRepo = '456569976649.dkr.ecr.eu-west-1.amazonaws.com/setl'
  ext.googleGuavaVersion = '29.0-jre'
  ext.jacksonVersion = '2.11.3'
  ext.log4j2Version = "2.17.1"
  ext.postgresVersion = "42.2.6"
  ext.prngVersion = '0.13'
  ext.pychainVersion = '222-SNAPSHOT'
  ext.setlCommons = '1.13'
  ext.setlPasswdCommon = '1.9'
  ext.setlHttpSignatures = '2.1.3'
  ext.slf4jVersion = "1.7.30"
  ext.springBootVersion = "2.5.4"
  ext.springSecurityVersion = '5.5.2'
  ext.springVersion = '5.3.11'
  ext.swaggerGradleVersion = '2.0.9.1'
  ext.swaggerVersion = '2.0.9'
  ext.utilMsgpackVersion = '2.0'
  ext.corda_release_group = "net.corda"
  ext.corda_core_release_group = "net.corda"
  ext.corda_release_version = "4.3"
  ext.corda_core_release_version = "4.3"
  ext.corda_gradle_plugins_version = "5.0.4"
  ext.kotlin_version = "1.4.10"
  ext.corda_platform_version = 5
  ext.quasar_version = "0.7.10"
  ext.junit_version = "4.12"
  ext.h2Version = '2.1.210'
}

subprojects {
  apply from: "${rootProject.projectDir}/repositories.gradle"
  
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'jacoco'
  apply plugin: 'com.github.spotbugs'
  apply plugin: 'checkstyle'
  apply plugin: 'maven-publish'
  apply plugin: "info.solidsoft.pitest"
  apply plugin: "org.owasp.dependencycheck"
  apply plugin: "io.freefair.lombok"

  project.group =  project(":").group
  project.version = project(":").version

  repositories {
    mavenLocal()
    mavenCentral()

    jcenter()
    maven { url 'https://software.r3.com/artifactory/corda' }
    maven { url 'https://jitpack.io' }

    if( ! new File('libs').isDirectory() ) {
      maven { url "http://si-nexus01.dev.setl.io:8081/repository/maven-public/" }
    }
  }

  configurations {
    all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
    all*.exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
    all*.resolutionStrategy {
      force group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4j2Version
      force group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j2Version
      force group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j2Version
      force group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
      force group: 'org.springframework', name: 'spring-core', version: springVersion
      force group: 'org.springframework', name: 'spring-beans', version: springVersion
      force group: 'org.springframework', name: 'spring-context', version: springVersion
      force group: 'org.springframework', name: 'spring-aop', version: springVersion

      // jackson core
      force group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
      force group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
      force group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion

      // jackson data types
      force group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: jacksonVersion
      force group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
      force group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: jacksonVersion

      // jackson modules
      force group: 'com.fasterxml.jackson.module', name: 'jackson-module-scala_2.13', version: jacksonVersion
      force group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: jacksonVersion

      // bouncy castle
      force group: 'org.bouncycastle', name: 'bcprov-debug-jdk15on', version: bouncyCastleVersion
      force group: 'org.bouncycastle', name: 'bctls-jdk15on', version: bouncyCastleVersion
      force group: 'org.bouncycastle', name: 'bcutil-jdk15on', version: bouncyCastleVersion
      force group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: bouncyCastleVersion

      // kotlin
      force group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
      force group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
      force group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-common', version: kotlin_version
      force group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk7', version: kotlin_version
      force group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
      force group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: kotlin_version
      force group: 'org.jetbrains.kotlin', name: 'kotlin-test-common', version: kotlin_version

      // h2 database
      force group: 'com.h2database', name: 'h2', version: h2Version
    }
  }

  sonarqube {
    properties {
      property "sonar.dependencyCheck.jsonReportPath", "$buildDir/reports/dependency-check-report.json"
      property "sonar.dependencyCheck.xmlReportPath", "$buildDir/reports/dependency-check-report.xml"
      property "sonar.dependencyCheck.htmlReportPath", "$buildDir/reports/dependency-check-report.html"
      property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/test/jacocoTestReport.xml"
    }
  }

  configurations.compile {
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'net.jcip', module: 'jcip-annotations'
  }

  configurations.testCompile {
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
    exclude group: 'net.jcip', module: 'jcip-annotations'
    exclude group: 'org.hamcrest', module: 'hamcrest-core'
  }

  lombok {
    version = "1.18.22"
  }

  //Checkstyle
  checkstyle {
    toolVersion = "8.41.1"
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
  }

  checkstyleTest {
    enabled = false
  }

  spotbugsMain {
    reports {
      html {
        enabled = false
      }
      xml {
        enabled = true
        destination = file("$project.buildDir/findbugsReports/main.xml")
      }
    }
  }

  spotbugs {
    ignoreFailures = true
  }
  spotbugsTest {
    enabled = false
  }

  jacoco {
    toolVersion = "0.8.5"
  }

  test {
    exclude '**/*TestSuite.class'
    finalizedBy jacocoTestReport
  }

  jacocoTestReport {
    reports {
      xml.enabled true
      html.enabled true
    }
    dependsOn test
  }

  dependencies {
    implementation group: 'org.springframework.kafka', name: 'spring-kafka', version: '2.7.8'
    implementation group: 'org.springframework', name: 'spring-context', version: "${springVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-actuator', version: "${springBootVersion}"

    implementation group: 'org.apache.kafka', name: 'kafka-streams', version: '2.8.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'com.google.jimfs', name: 'jimfs', version: '1.1'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
    compile(group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1') {
      exclude module: 'com.google.code.findbugs:jsr305'
    }
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourceJar {
          classifier "sources"
        }
      }
    }
    repositories {
      maven {
        credentials {
          username = project.hasProperty('mavenUser') ? "$mavenUser" : ''
          password = project.hasProperty('mavenPassword') ? "$mavenPassword" : ''
        }
        if (version.endsWith("SNAPSHOT")) {
          url 'http://si-nexus01.dev.setl.io:8081/repository/setl-snaps'
        } else {
          url 'http://si-nexus01.dev.setl.io:8081/repository/setl-libs'
        }
      }
    }
  }

  task allDependencies(type: DependencyReportTask) {}
}
